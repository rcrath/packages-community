# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Scott Furry <scott.wl.furry@gmail.com>

pkgname=guayadeque
pkgver=0.4.5.r55.g7b6d34db
pkgrel=1
_commit=7b6d34db78bc8cd6e36c3815dbac11b321eee115
pkgdesc='Lightweight music player'
arch=('i686' 'x86_64')
url='http://guayadeque.org/'
license=('GPL3')
depends=('curl'
        'dbus'
        'glib2'
        'gst-plugins-base'
        'gst-plugins-good'
        'gst-plugins-bad'
        'gst-plugins-ugly'
        'gstreamer'
        'libgpod'
        'taglib'
        'wxsqlite3')
makedepends=('cmake')
optdepends=('gvfs: Support for external devices')
source=("${pkgname}-${pkgver}-${pkgrel}.tar.gz::https://github.com/anonbeat/${pkgname}/archive/$_commit.tar.gz")
sha256sums=('8de0e8debb9b5c6bd29209c2424dd138574d11df79579814b33564ffc68884e3')

build() {
  cd ${pkgname}-${_commit}
  ./buildt
  cmake . \
    -DCMAKE_CXX_STANDARD='11' \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DwxWidgets_wxrc_EXECUTABLE='/usr/bin/wxrc-3.0' \
    -DwxWidgets_CONFIG_EXECUTABLE='/usr/bin/wx-config-gtk3' \
    -DwxWidgets_INCLUDE_DIRS='/usr/include/wx-3.0/'
  make
}

package() {
  cd ${pkgname}-${_commit}
  make DESTDIR="${pkgdir}" install
}
