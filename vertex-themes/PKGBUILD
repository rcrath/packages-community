# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Author: horst3180 @ deviantart

_repo=Vertex-theme
pkgname="${_repo,}s"
pkgver=20180424
pkgrel=2
pkgdesc='Vertex Gtk2, Gtk3, Metacity, Xfwm, Cinnamon and GNOME Shell themes.'
_gnomever=3.22
arch=('any')
url="http://horst3180.deviantart.com/art/Vertex-Theme-470663601"
_url="https://github.com/oberon2007/$_repo"
license=('GPL3')
depends=('gtk-engine-murrine')
makedepends=('git')
optdepends=('ff-theme-util: set Firefox and Palemoon themes automatically')
source=("git+$_url.git")
sha256sums=('SKIP')

pkgver() {
    date +%Y%m%d
}

build() {
  cd $_repo
  ./autogen.sh --prefix=/usr --disable-unity --with-gnome=${_gnomever}
}
package() {
  cd $_repo

  make DESTDIR="$pkgdir" install
  cd extra
  for name in "Chrome" "Firefox" "Vertex_alt_metacity"; do
    find "$name" -type f -not -name *~ -exec install -Dm644 '{}' "$pkgdir/usr/share/themes/{}" \;
  done
  install -Dm644 Vertex-Plank/dock.theme $pkgdir/usr/share/plank/themes/Vertex/dock.theme
  
  cd ..
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  install -Dm644 README.md "$pkgdir/usr/share/themes/Vertex/README"
}
