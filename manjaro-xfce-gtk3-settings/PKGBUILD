# Maintainer: Ramon Buld√≥ <rbuldo@gmail.com>

pkgbase=manjaro-xfce-gtk3-settings
pkgname=('manjaro-xfce-gtk3-settings'
         'manjaro-xfce-gtk3-minimal-settings')
pkgver=20180330
pkgrel=2
pkgdesc="Manjaro Linux Xfce settings"
arch=('any')
url="https://github.com/manjaro/manjaro-xfce-settings"
license=('GPL')
makedepends=('git')
source=("$pkgbase::git+$url.git" 'manjaro-xfce-settings.hook' 'mxs-bgd-sym')
sha256sums=('SKIP'
            '712be2c2c621bdf1420a78188af49420bf31d28638283f29107c5274bed3ddfe'
            '03770c4735b588bdee49f7e1e5d73d8500524d875603c473bdf0f4ffb862c152')

pkgver() {
	date +%Y%m%d
}

_inst_dir(){
	install -d $pkgdir/etc
	cp -r $srcdir/$pkgbase/skel $pkgdir/etc

	install -d $pkgdir/usr/share/glib-2.0/schemas
	cp $srcdir/$pkgbase/schemas/* $pkgdir/usr/share/glib-2.0/schemas
}

package_manjaro-xfce-gtk3-settings() {
	pkgdesc='Manjaro Linux XFCE-GTK3 Settings'
	depends=('breath-wallpaper'
	         'manjaro-base-skel'
	         'manjaro-icons'
	         'qt5ct'
	         'qt5-styleplugins'
	         'vibrancy-colors'
	         'adapta-maia-theme'
	         'kvantum-qt5'
                 'kvantum-manjaro'
	         'xcursor-breeze')
	conflicts=('manjaro-desktop-settings' 'manjaro-xfce-settings')
	provides=('manjaro-desktop-settings' 'manjaro-xfce-settings')

	_inst_dir
	local _subst="
		s|%BIN%|mxs-bgd-sym|g
	"
	sed "${_subst}" "${srcdir}/manjaro-xfce-settings.hook" |
		install -Dm644 /dev/stdin "${pkgdir}/usr/share/libalpm/hooks/manjaro-xfce-settings.hook"
	install -Dm755 "${srcdir}/mxs-bgd-sym" "${pkgdir}/usr/bin/mxs-bgd-sym"
}

#############minimal######################

package_manjaro-xfce-gtk3-minimal-settings() {
	pkgdesc='Manjaro Linux xfce-minimal settings'
	depends=('breath-wallpaper'
	         'manjaro-base-skel'
	         'manjaro-icons'
	         'qt5ct'
	         'qt5-styleplugins'
	         'vertex-maia-icon-theme'
	         'vertex-maia-themes'
	         'xcursor-breeze')
	conflicts=('manjaro-desktop-settings' 'manjaro-xfce-minimal-settings')
	provides=('manjaro-desktop-settings' 'manjaro-xfce-minimal-settings')

	_inst_dir '/minimal'
	local _subst="
		s|%BIN%|mxms-bgd-sym|g
	"
	sed "${_subst}" "${srcdir}/manjaro-xfce-settings.hook" |
		install -Dm644 /dev/stdin "${pkgdir}/usr/share/libalpm/hooks/manjaro-xfce-minimal-settings.hook"
	install -Dm755 "${srcdir}/mxs-bgd-sym" "${pkgdir}/usr/bin/mxms-bgd-sym"
}
