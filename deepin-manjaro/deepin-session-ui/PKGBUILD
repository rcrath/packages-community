# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-session-ui
pkgver=4.2.0
pkgrel=1.2
pkgdesc='Deepin desktop-environment - Session UI module'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-session-ui"
license=('GPL3')
groups=('deepin')
depends=('gsettings-qt' 'deepin-qt5integration' 'liblightdm-qt5' 'qt5-svg' 'deepin-daemon'
        'xorg-server' 'deepin-control-center' 'startdde' 'xorg-xrandr' 'deepin-qt-dbus-factory')
makedepends=('qt5-tools' 'deepin-gettext-tools')
provides=('lightdm-deepin-greeter')
conflicts=('dde-workspace' 'dde-session-ui')
replaces=('dde-workspace' 'dde-session-ui')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-session-ui/archive/$pkgver.tar.gz"
    'rm_unneeded.patch')
sha256sums=('afc415b58d88dd5da15cfa18642503110711ecb1854827a4c2b7853e8497c855'
            '1d4100a57debbb8cd1a5f3828d737fd4539cf0ad3e9c50706c22d78e6562560e')
prepare() {
  cd dde-session-ui-$pkgver
  patch -p1 -i $srcdir/rm_unneeded.patch
}

build() {
  cd dde-session-ui-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-session-ui-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
