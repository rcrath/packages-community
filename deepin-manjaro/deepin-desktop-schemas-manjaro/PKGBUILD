# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

_pkgname=deepin-desktop-schemas
pkgname=$_pkgname-manjaro
pkgver=3.2.10
pkgrel=1
pkgdesc='Manjaro Linux Deepin gschemas'
arch=('any')
url="https://github.com/linuxdeepin/$_pkgname"
license=('GPL3')
groups=('deepin-manjaro')
depends=('dconf'
    'deepin-iconthemes-manjaro'
    'deepin-sound-theme'
    'deepin-wallpapers'
    'noto-fonts')
makedepends=('git')
conflicts=("$_pkgname"
    'deepin-artwork-themes')
replaces=('deepin-artwork-themes' 'deepin-artwork-themes-manjaro')
provides=("$_pkgname")
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-desktop-schemas/archive/$pkgver.tar.gz"
    'manjaro.diff')
sha256sums=('683c7f6e5a33db50b3a01e8acc69a0ca2ab734d65f6936d7c1491c2918cc5d20'
            'c8b465e80bf72636768b156eb99086a3c428e32e3e05ba0171212c01da7271d0')

prepare() {
    cd $_pkgname-$pkgver
    patch -p1 < ../manjaro.diff
}

build() {
    cd $_pkgname-$pkgver
    make ARCH=x86
}

package() {
    cd $_pkgname-$pkgver
    make DESTDIR=$pkgdir install
}
