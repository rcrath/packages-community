# Maintainer: FadeMind <fademind@gmail.com>

pkgname=copyq
pkgver=3.4.0
pkgrel=1
pkgdesc="Clipboard manager with searchable and editable history."
url="https://github.com/hluk/CopyQ"
arch=('x86_64')
license=('GPL3')
depends=('libxtst' 'qt5-script' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('cmake' 'qt5-tools' 'qt5-svg')
replaces=('copyq-git' 'copyq-plugin-itemweb-git' 'copyq-plugin-itemweb')
conflicts=('copyq-git' 'copyq-plugin-itemweb-git' 'copyq-plugin-itemweb')
source=("${url}/archive/v${pkgver}.tar.gz")
sha256sums=('2e9b5975310a46bfe39873392a1b27ef36be96783608440317885a8c140ab280')

prepare() {
	mv CopyQ-${pkgver} ${pkgname}
	cd ${pkgname}
    if [[ -d build ]]; then
        rm -Rf build
    fi
    mkdir -p build
}

build() {
    cd ${pkgname}/build
    cmake \
    	-DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DWITH_QT5=TRUE \
        ${srcdir}/${pkgname}
    make
}

package() {
    cd ${pkgname}/build
    make DESTDIR="${pkgdir}" install
}

