# Maintainer: FadeMind <fademind@gmail.com>
# Contributor: Ashley Whetter <(firstname) @ awhetter.co.uk>
# Contributor: Eothred <yngve.levinsen@gmail.com>

# Check for new Linux releases:
# $ wget "http://repository.spotify.com/pool/non-free/s/spotify-client/" -q -O /tmp/sv.html && awk '{print $2;}' /tmp/sv.html |cut -b 7-50|grep spotify;rm /tmp/sv.html

pkgname=spotify
pkgver=1.0.77.338
_anotherpkgver=.g758ebd78
_amd64_pkgrel=41
pkgrel=1
pkgdesc="A proprietary music streaming service"
arch=('x86_64')
license=('custom:"Copyright (c) 2006-2018 Spotify Ltd"')
url="https://www.spotify.com"
depends=("alsa-lib>=1.0.14" "gconf" "gtk2" "glib2" "nss" "libsystemd" "libxtst" "libx11" "libxss" "openssl-1.0" "libcurl-gnutls" "desktop-file-utils" "rtmpdump")
optdepends=('ffmpeg-compat-54: Adds support for playback of local files'
            'zenity: Adds support for importing local files'
            'libnotify: Desktop notifications')
options=('!strip' '!upx')
install='spotify.install'
source=("http://repository.spotify.com/pool/non-free/s/spotify-client/spotify-client_${pkgver}${_anotherpkgver}-${_amd64_pkgrel}_amd64.deb"
        'clean-sc'
        'spotify.protocol'
        'LICENSE')
sha256sums=('0698d2dbda43f2866d2a6f6ed9816bf8022aa8cf67d0dba347f486410093e1a4'
            '2bd1072e1c9d0466e0f64a99e5d5b3c558c596b59a9730e627e65e57e02caa87'
            'af54f3b90cac46fa100b3f919a9225d10d847617d24aa9af3d832e7689f482c3'
            '4e8bea31ca27e16cac9c9dcd8f6ec27e1f82b45de86d6fee7a1e77e23f884b92')

package() {
	cd "${srcdir}"

	tar -xzf data.tar.gz -C "${pkgdir}"

	install -D "${pkgdir}"/usr/share/spotify/spotify.desktop "${pkgdir}"/usr/share/applications/spotify.desktop
	install -D "${pkgdir}"/usr/share/spotify/icons/spotify-linux-512.png "${pkgdir}"/usr/share/pixmaps/spotify-client.png

	for size in 22 24 32 48 64 128 256 512; do
		install -D "${pkgdir}/usr/share/spotify/icons/spotify-linux-$size.png" \
			"${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/spotify.png"
	done

	chmod -R go-w "${pkgdir}"/usr
	install -Dm755 "${srcdir}/clean-sc" "${pkgdir}/usr/bin/clean-sc"
	install -Dm644 "${srcdir}/spotify.protocol" "${pkgdir}/usr/share/kservices5/spotify.protocol"
	install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
