# Maintainer: FadeMind <fademind@gmail.com>
# Contributor: Ashley Whetter <(firstname) @ awhetter.co.uk>
# Contributor: Eothred <yngve.levinsen@gmail.com>

# Check for new Linux releases:
# $ wget "http://repository.spotify.com/pool/non-free/s/spotify-client/" -q -O /tmp/sv.html && awk '{print $2;}' /tmp/sv.html |cut -b 7-50|grep spotify;rm /tmp/sv.html

pkgname=spotify
pkgver=1.0.79.223
pkgrel=1
_pkgver_x86_64=1.0.79.223
_commit_x86_64=.g92622cc2
_pkgrel_x86_64=21
_pkgver_i686=1.0.72.117
_commit_i686=.g6bd7cc73
_pkgrel_i686=35
pkgdesc="A proprietary music streaming service"
arch=('x86_64' 'i686')
license=('custom:"Copyright (c) 2006-2018 Spotify Ltd"')
url="https://www.spotify.com"
options=('!strip' '!upx')
install='spotify.install'

source=('spotify'
        'spotify.protocol'
        'LICENSE'
        'clean-sc')
sha256sums=('989920e9360cadc1a8103b8c04acf0c87cb7911eb9a09dddb0cf4708d6249d34'
            'af54f3b90cac46fa100b3f919a9225d10d847617d24aa9af3d832e7689f482c3'
            '4e8bea31ca27e16cac9c9dcd8f6ec27e1f82b45de86d6fee7a1e77e23f884b92'
            '2bd1072e1c9d0466e0f64a99e5d5b3c558c596b59a9730e627e65e57e02caa87')
sha256sums_x86_64=('86b7f2c83a69cf4b3ccbb4c5b3c3f4a0c14b961980ffabc3af8cd8f8f3bd39f4')
sha256sums_i686=('f5ac29e8374901ce7401825d13471c03bcf6ec8106f8c210c710b0a8d7b22ca9')

source_x86_64=("http://repository.spotify.com/pool/non-free/s/spotify-client/spotify-client_${_pkgver_x86_64}${_commit_x86_64}-${_pkgrel_x86_64}_amd64.deb")
source_i686=("http://repository.spotify.com/pool/non-free/s/spotify-client/spotify-client_${_pkgver_i686}${_commit_i686}-${_pkgrel_i686}_i386.deb")

depends=("alsa-lib>=1.0.14" "gconf" "gtk2" "glib2" "nss" "libsystemd" "libxtst" "libx11" "libxss" "openssl-1.0" "desktop-file-utils" "rtmpdump")
depends_x86_64=('libcurl-gnutls')
depends_i686=('libcurl-compat')
optdepends=('ffmpeg-compat-54: Adds support for playback of local files'
            'zenity: Adds support for importing local files'
            'libnotify: Desktop notifications')

pkgver() {
	if [[ $CARCH == "x86_64" ]] ; then
		echo $_pkgver_x86_64
	else
		echo $_pkgver_i686
	fi
}

package() {
	cd "${srcdir}"

	tar -xzf data.tar.gz -C "${pkgdir}"

	install -D "${pkgdir}"/usr/share/spotify/spotify.desktop "${pkgdir}"/usr/share/applications/spotify.desktop
	install -D "${pkgdir}"/usr/share/spotify/icons/spotify-linux-512.png "${pkgdir}"/usr/share/pixmaps/spotify-client.png

	for size in 22 24 32 48 64 128 256 512; do
		install -D "${pkgdir}/usr/share/spotify/icons/spotify-linux-$size.png" \
			"${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/spotify.png"
	done

	chmod -R go-w "${pkgdir}"/usr

	# Wrapper script for i686
	if [[ "$CARCH" == "i686" ]] ; then
		rm "${pkgdir}"/usr/bin/spotify
		install -Dm755 "${srcdir}/spotify" "${pkgdir}/usr/bin/spotify"
	fi

	# Copy protocol file for KDE
	install -Dm644 "${srcdir}/spotify.protocol" "${pkgdir}/usr/share/kservices5/spotify.protocol"

	# License
	install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	
	# "Clean Spotify Cache" script
	install -Dm755 "${srcdir}/clean-sc" "${pkgdir}/usr/bin/clean-sc"
}
