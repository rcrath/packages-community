# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Author: horst3180 @ deviantart
# Contributor: Germ√°n Franco <dev.germanfr@gmail.com>
# Contributor: @Photon89

pkgname=vertex-theme
pkgver=20180429
pkgrel=1
pkgdesc='Vertex Gtk2, Gtk3, Metacity, Xfwm, Cinnamon and GNOME Shell themes.'
_gnomever=3.22
arch=('any')
url="http://horst3180.deviantart.com/art/Vertex-Theme-470663601"
_url="https://github.com/oberon2007/$pkgname"
license=('GPL3')
depends=('gtk-engine-murrine')
conflicts=('vertex-themes')
replaces=('vertex-themes')
source=("$_url/archive/$pkgver.tar.gz")
md5sums=('40e863530c2b015dd0fef5dcedd34195')

build() {
  cd $pkgname-$pkgver
  ./autogen.sh \
    --prefix=/usr \
    --disable-unity \
    --with-gnome=${_gnomever}
}
package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
  cd extra
  for name in "Chrome" "Firefox" "Vertex_alt_metacity"; do
    find "$name" -type f -not -name *~ -exec install -Dm644 '{}' "$pkgdir/usr/share/themes/{}" \;
  done
  install -Dm644 Vertex-Plank/dock.theme $pkgdir/usr/share/plank/themes/Vertex/dock.theme
  
  cd ..
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  install -Dm644 README.md "$pkgdir/usr/share/themes/Vertex/README"
}
