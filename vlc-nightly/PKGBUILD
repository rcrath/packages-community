# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Sebastian Weiland <sebastianweiland97@gmail.com>
# Contributor: Martchus <martchus@gmx.net>

pkgname=vlc-nightly
_pkgname=vlc
_pkgver=3.0.3
_pkgdate=20180520
_pkgtime=0225
_pkgsuffix=   

pkgver=${_pkgver}v${_pkgdate}
pkgrel=1

pkgdesc="A multi-platform MPEG, VCD/DVD, and DivX player - nightly snapshot"
arch=('i686' 'x86_64')
url="http://www.videolan.org/vlc/"
license=('LGPL2.1' 'GPL2')
depends=('a52dec'
         'daala'
         'faad2'
         'ffmpeg'
         'fribidi'
         'hicolor-icon-theme'
         'libarchive'
         'libdca'
         'libdvbpsi'
         'libdvdnav'
         'libfdk-aac'
         'libglvnd'
         'libmad'
         'libmatroska'
         'libmpcdec'
         'libmpeg2'
         'libproxy'
         'libsecret'
         'libshout'
         'libtar'
         'libtiger'
         'libupnp'
         'libxinerama'
         'libxpm'
         'lua'
         'mesa'
         'qt5-base'
         'qt5-svg'
         'qt5-x11extras'
         'sdl_image'
         'sidplay2-libs'
         'sndio'
         'taglib'
         'wayland'
         'wayland-protocols'
         'xcb-util-keysyms'
         'zvbi')
makedepends=('aalib'
             'flac'
             'git'
             'libavc1394'
             'libbluray'
             'libcaca'
             'libdc1394'
             'libdvdcss'
             'libgme'
             'libgoom2'
             'libmtp'
             'libnotify'
             'librsvg'
             'libssh2'
             'lirc'
             'live-media'
             'opus'
             'portaudio'
             'projectm'
             'smbclient'
             'twolame'
             'vcdimager')
optdepends=('aalib: for ASCII art plugin'
            'avahi: for service discovery using bonjour protocol'
            'flac: for Free Lossless Audio Codec plugin'
            'kdelibs: KDE Solid hardware integration'
            'libavc1394: for devices using the 1394ta AV/C'
            'libbluray: for Blu-Ray support'
            'libcaca: for colored ASCII art video output'
            'libcdio: for audio CD playback support'
            'libdc1394: for IEEE 1394 plugin'
            'libdvdcss: for decoding encrypted DVDs'
            'libgme: for libgme plugin'
            'libgoom2: for libgoom plugin'
            'libmtp: for MTP devices support'
            'libnotify: for notification plugin'
            'librsvg: for SVG plugin'
            'libssh2: for sftp support'
            'libva-intel-driver: back-end for intel cards'
            'libva-vdpau-driver: vdpau back-end for nvidia'
            'lirc: for lirc plugin'
            'lua-socket: for http interface'
            'ncurses: for ncurses interface support'
            'opus: for opus support'
            'portaudio: for portaudio support'
            'projectm: for ProjectM visualisation plugin'
            'smbclient: for SMB access plugin'
            'ttf-dejavu: for subtitle font'
            'ttf-freefont: for subtitle font'
            'twolame: for TwoLAME mpeg2 encoder plugin'
            'vcdimager: navigate VCD with libvcdinfo')
conflicts=("vlc-plugin" "vlc")
provides=("vlc")
replaces=("vlc-plugin")
options=('!libtool' '!emptydirs' 'debug')
source=("https://nightlies.videolan.org/build/source/${_pkgname}-${_pkgver}-${_pkgdate}-${_pkgtime}${_pkgsuffix}.tar.xz"
        'update-vlc-plugin-cache.hook'
        'lua53_compat.patch::https://git.archlinux.org/svntogit/packages.git/plain/trunk/lua53_compat.patch?h=packages/vlc'
        'prevent-including-missing-SVGs.patch')

sha256sums=('21df7ac2389a23281ba50171195687b55a81d9707308bcb0d905ea9f2f8caf88'
            'c6f60c50375ae688755557dbfc5bd4a90a8998f8cf4d356c10d872a1a0b44f3a'
            'd1cb88a1037120ea83ef75b2a13039a16825516b776d71597d0e2eae5df2d8fa'
            '599ce4bf0db5fc74f54bc03136b662606c22b8435b81440b02fc73f52d09bd7f')

pkgver() {
	echo "${_pkgver}v${_pkgdate}"
}

prepare() {
    cd ${_pkgname}-${_pkgver}${_pkgsuffix}
    ./bootstrap

    patch -Np1 -i $srcdir/lua53_compat.patch
    patch -p1 -i "$srcdir/prevent-including-missing-SVGs.patch"
    sed -i -e 's|-Werror-implicit-function-declaration||g' configure
    sed -i -e 's:truetype/ttf-dejavu:TTF:g' modules/visualization/projectm.cpp
    sed -i -e 's:truetype/freefont:TTF:g' modules/text_renderer/freetype/freetype.c

    sed -i -e 's|advprefs_audio.png|advprefs_audio.svg|' modules/gui/qt/vlc.qrc
    sed -i -e 's|advprefs_codec.png|advprefs_codec.svg|' modules/gui/qt/vlc.qrc
    sed -i -e 's|advprefs_extended.png|advprefs_extended.svg|' modules/gui/qt/vlc.qrc
    sed -i -e 's|advprefs_intf.png|advprefs_intf.svg|' modules/gui/qt/vlc.qrc
    sed -i -e 's|advprefs_playlist.png|advprefs_playlist.svg|' modules/gui/qt/vlc.qrc
    sed -i -e 's|advprefs_sout.png|advprefs_sout.svg|' modules/gui/qt/vlc.qrc
    sed -i -e 's|advprefs_video.png|advprefs_video.svg|' modules/gui/qt/vlc.qrc
    sed -i -e 's|win7thumbnail_pause.png|win7thumbnail_pause.svg|' modules/gui/qt/vlc.qrc
    sed -i -e 's|win7thumbnail_prev.png|win7thumbnail_prev.svg|' modules/gui/qt/vlc.qrc
    sed -i -e 's|win7thumbnail_play.png|win7thumbnail_play.svg|' modules/gui/qt/vlc.qrc
    sed -i -e 's|win7thumbnail_next.png|win7thumbnail_next.svg|' modules/gui/qt/vlc.qrc
}

build() {
    cd ${_pkgname}-${_pkgver}${_pkgsuffix}
    export CFLAGS+=" -I/usr/include/samba-4.0"
    export CPPFLAGS+=" -I/usr/include/samba-4.0" 
    export CXXFLAGS+=" -std=gnu++11"
    export LUAC=/usr/bin/luac
    export LUA_LIBS="`pkg-config --libs lua`"
    export RCC=/usr/bin/rcc-qt5

    ./configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --disable-rpath \
      --enable-faad \
      --enable-nls \
      --enable-lirc \
      --enable-ncurses \
      --enable-realrtsp \
      --enable-aa \
      --enable-upnp \
      --enable-opus \
      --enable-sftp \
      --enable-fdkaac \
      --enable-archive \
      --enable-bluray \
      --enable-daala

    ./compile
}

package() {
    cd ${_pkgname}-${_pkgver}${_pkgsuffix}
    make -i DESTDIR="$pkgdir" install

    for res in 16 32 48 128; do
        install -D -m644 share/icons/${res}x$res/vlc.png \
          $pkgdir/usr/share/icons/hicolor/${res}x$res/apps/vlc.png
    done

    install -Dm644 $srcdir/update-vlc-plugin-cache.hook \
      $pkgdir/usr/share/libalpm/hooks/update-vlc-plugin-cache.hook
}
