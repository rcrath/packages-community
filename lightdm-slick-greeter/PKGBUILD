# Maintainer: Stefano Capitani <stefanoatmanjarodotorg>
# Maintainer: Sam Burgos < sam dot burgos1089 at gmail dot com >

pkgname='lightdm-slick-greeter'
_pkgname='slick-greeter'
pkgver=1.1.4
pkgrel=7
pkgdesc='A slick-looking LightDM greeter'
arch=(i686 x86_64)
url="https://github.com/linuxmint/${_pkgname}"
license=('GPL3')
source=("${url}/archive/${pkgver}.tar.gz"
    "${_pkgname}.conf"
    "${_pkgname}.png"
    'schema-defaults.patch')
depends=('cairo' 'freetype2' 'gtk3' 'libcanberra' 'libxext' 'lightdm' 'pixman')
makedepends=('intltool' 'gnome-common' 'vala')
backup=('etc/lightdm/slick-greeter.conf')
sha256sums=('a73e5f8a448731b6497fbf6a6599a38892ea316f6c075e3b63a14911ffdf565d'
            'a015a40fcd2ba09d3744aff7126041d3f2ce2ae6e2939f07b45cd7ae0a482894'
            '1295fc79a111af0834ab95ea2a3a9d2684c42a062b21d88a1a0c88da9ed5ca16'
            '119e0b5f449a66946ae257ce80d6a7b2a19d2f9bdfd53c2a52c1a47b676829c3')
install=slick-greeter.install

prepare() {
    cd ${_pkgname}-${pkgver}
    patch -p1 -i ../schema-defaults.patch
}

build() {
    cd ${_pkgname}-${pkgver}
    aclocal --install
    autoreconf -vfi
    intltoolize -f
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --sbindir=/usr/bin \
        --libexecdir=/usr/lib/lightdm
    make
}

package() {
    cd ${_pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
    # adjust launcher name
    mv $pkgdir/usr/share/xgreeters/slick-greeter.desktop \
      $pkgdir/usr/share/xgreeters/lightdm-slick-greeter.desktop
    # Install default conf 
    install -Dm644 $srcdir/$_pkgname.conf $pkgdir/etc/lightdm/$_pkgname.conf
    install -Dm644 $srcdir/$_pkgname.png $pkgdir/usr/share/slick-greeter/$_pkgname.png
}
