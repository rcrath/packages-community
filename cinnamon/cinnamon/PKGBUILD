# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Alexandre Filgueira <alexfilgueira@cinnarch.com>

pkgname=cinnamon
pkgver=3.6.7
pkgrel=4
pkgdesc="Linux desktop which provides advanced innovative features and a traditional user experience"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/Cinnamon"
_url="https://github.com/oberon2007/Cinnamon"
_commit=2962ef882905bd759d870df494c8a50d81623899
license=('GPL2')
depends=('accountsservice'
    'caribou'
    'cinnamon-settings-daemon'
    'cinnamon-session'
    'cinnamon-translations'
    'cjs'
    'clutter-gtk'
    'gettext'
    'gnome-backgrounds'
    'gnome-themes-standard'
    'gstreamer'
    'iso-flag-png'
    'libkeybinder3'
    'librsvg'
    'muffin>=3.8'
    'python2-cairo'
    'python-dbus'
    'python2-dbus'
    'python2-pillow'
    'python2-pam'
    'python2-pexpect'
    'python2-pyinotify'
    'python2-lxml'
    'cinnamon-control-center'
    'cinnamon-screensaver'
    'cinnamon-menus'
    'libgnomekbd'
    'network-manager-applet'
    'nemo'
    'polkit-gnome'
    'xapps'
    'xdg-utils'
    'python2-gobject')
makedepends=('autoconf-archive'
    'git'
    'gnome-common'
    'intltool'
    'gobject-introspection')
optdepends=('blueberry: Bluetooth support'
    'gnome-panel: fallback mode'
    'metacity: fallback mode'
    'system-config-printer: printer settings'
    'tint2: fallback mode')
options=('!emptydirs')
source=( #"$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz"
    "git+$_url#commit=$_commit"
    "set_wheel.patch"
    "default-theme.patch"
    'add-msm.patch'
    "user-applet.patch::$url/commit/12ac916942e9fb15d6e94bbff73d7a645818a70f.patch"
    "muffin-3.8.patch::$url/commit/5c9848a48da3387825f627117ad58521474e0b2e.patch"
    "cs_user.py-add-compatibility-for-multiple-pam-module.patch"
    "cinanmon-settings-don-t-rely-on-the-presence-of-cinn.patch::$url/pull/7382.patch"
)
sha256sums=('SKIP'
            '5890a3723c28a6679db92cbd87d7e06ea5e5ecf6b7be3494b38decbe814c1f0e'
            'bb60a2bb27efbef132798677f61ac46aa6d17f5432f0f81117ec97c1b4601aad'
            '6bd365915fc400be2e245104659744a038c61fe618c47d96977d750ba3e4e3d7'
            'cbac2c3ae5a5180464b6bd50bd2a360b2351a3af309074d5ef9386119470abe5'
            '6b7288144a650d1af6d7e589b671594f1ce57c01ce94e770dc7f89d9cf184641'
            '1d87754153369acb9fc6e8a301ba6194907ebc27fb8183f5787700880f76be36'
            'd46b2d6c60b52aa87f5a30568c2106d77b45c0febe8bf387ffd201b5b4f5a101')
            
prepare() {
  cd ${srcdir}/Cinnamon*

  # https://github.com/linuxmint/Cinnamon/issues/7497 Work with muffin 3.8
  patch -p1 < ../muffin-3.8.patch

  # Backport https://github.com/linuxmint/Cinnamon/pull/7327
  patch -p1 < ../cs_user.py-add-compatibility-for-multiple-pam-module.patch

  # Check for the cc-panel module path, not for the irrelevant binary
  patch -p1 < ../cinanmon-settings-don-t-rely-on-the-presence-of-cinn.patch

  # Use wheel group instread of sudo (taken from Fedora)
  patch -Np1 -i ../set_wheel.patch

  # Set default theme to 'cinnamon'
  patch -Np1 -i ../default-theme.patch
  
  # add MSM to cinnamon-settings
  patch -Np1 -i ../add-msm.patch

  # user-applet: fix loading with newer accountsservice
  patch -Np1 -i ../user-applet.patch

  # Add polkit agent to required components
  sed -i 's/RequiredComponents=\(.*\)$/RequiredComponents=\1polkit-gnome-authentication-agent-1;/' \
    files/usr/share/cinnamon-session/sessions/cinnamon*.session

  # Use pkexec instead of gksu
  sed -i 's/gksu/pkexec/' files/usr/bin/cinnamon-settings-users

  # Check for the cc-panel path, not for the unneeded binary
  sed -i 's|/usr/bin/cinnamon-control-center|/usr/lib/cinnamon-control-center-1/panels|' \
    files/usr/bin/cinnamon-settings

  # Cinnamon has no upstream backgrounds, use GNOME backgrounds instead
  sed -i 's|/usr/share/cinnamon-background-properties|/usr/share/gnome-background-properties|' \
    files/usr/share/cinnamon/cinnamon-settings/modules/cs_backgrounds.py

  # GNOME Terminal desktop file was renamed in GNOME 3.20
  sed -i 's/gnome-terminal.desktop/org.gnome.Terminal.desktop/' data/org.cinnamon.gschema.xml.in \
    files/usr/share/cinnamon/applets/panel-launchers@cinnamon.org/settings-schema.json

  # Replace MintInstall with GNOME Software
  sed -i 's/mintinstall.desktop/org.gnome.Software.desktop/' data/org.cinnamon.gschema.xml.in

  # Remove broken symlink
  rm files/etc/xdg/menus/cinnamon-applications-merged
}

build() {
  cd ${srcdir}/Cinnamon*

  ./autogen.sh --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/cinnamon \
    --localstatedir=/var \
    --disable-static \
    --disable-schemas-compile \
    --enable-gtk-doc \
    --enable-compile-warnings=yes

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd ${srcdir}/Cinnamon*
  make DESTDIR="${pkgdir}" install

  # enable Adwaita themes
  mkdir -p $pkgdir/usr/share/themes/Adwaita{,-dark}/metacity-1/
  echo 'native' > $pkgdir/usr/share/themes/Adwaita/metacity-1/muffin-adwaita.txt
  echo 'native' > $pkgdir/usr/share/themes/Adwaita-dark/metacity-1/muffin-adwaita.txt
}
